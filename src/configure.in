dnl Process this file with autoconf to produce a configure script.

dnl $Id: configure.in,v 1.6 2003/05/18 00:04:39 fredette Exp $

dnl acinclude.m4 - additional tme autoconf macros:

dnl Copyright (c) 2001, 2003 Matt Fredette
dnl All rights reserved.
dnl
dnl Redistribution and use in source and binary forms, with or without
dnl modification, are permitted provided that the following conditions
dnl are met:
dnl 1. Redistributions of source code must retain the above copyright
dnl    notice, this list of conditions and the following disclaimer.
dnl 2. Redistributions in binary form must reproduce the above copyright
dnl    notice, this list of conditions and the following disclaimer in the
dnl    documentation and/or other materials provided with the distribution.
dnl 3. All advertising materials mentioning features or use of this software
dnl    must display the following acknowledgement:
dnl      This product includes software developed by Matt Fredette.
dnl 4. The name of the author may not be used to endorse or promote products
dnl    derived from this software without specific prior written permission.
dnl
dnl THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
dnl IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
dnl WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
dnl DISCLAIMED.  IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT,
dnl INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
dnl (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
dnl SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
dnl HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
dnl STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN
dnl ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
dnl POSSIBILITY OF SUCH DAMAGE.

dnl Checks that we are given a good source directory.
AC_INIT(ic/m68k/m68k-impl.h)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(tme, 0.0)

dnl Write configuration out to config.h through config.h.in.
AM_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_YACC
AC_PROG_INSTALL
AC_PROG_MAKE_SET

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h stdio.h memory.h stdarg.h)
AC_CHECK_HEADERS(sys/ioctl.h sys/sockio.h sys/socketio.h net/if_dl.h ioctls.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_BIGENDIAN
AC_C_CONST
AC_C_INLINE
AC_CHECK_SIZEOF(long, 4)
AC_CHECK_SIZEOF(int, 4)
AC_CHECK_SIZEOF(short, 2)
if test $ac_cv_sizeof_int != 4 && test $ac_cv_sizeof_long != 4; then
  AC_MSG_ERROR([can't find a 32-bit type])
fi
if test $ac_cv_sizeof_int != 2 && test $ac_cv_sizeof_short != 2; then
  AC_MSG_ERROR([can't find a 16-bit type])
fi
AC_CHECK_ALIGNOF(32)
AC_CHECK_ALIGNOF(16)
AC_CHECK_SHIFTMAX(8)
AC_CHECK_SHIFTMAX(16)
AC_CHECK_SHIFTMAX(32)
AC_SYS_SOCKADDR_SA_LEN

dnl Checks for library functions and prototypes.
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MEMCMP
AC_FUNC_MMAP

dnl Checks for perl.
AC_PATH_PROGS(PERL, perl4.036 perl4 perl perl5, no)
AC_SUBST(PERL)

dnl Checks for AF_LINK.
AC_MSG_CHECKING([for AF_LINK support])
AC_EGREP_CPP(_tme_has_af_link,
[
#include <sys/socket.h>
#ifdef AF_LINK
_tme_has_af_link
#endif
], [ 
AC_MSG_RESULT(yes)
AC_DEFINE(HAVE_AF_LINK, [], [Define if you have AF_LINK.])
], [
AC_MSG_RESULT(no)
])

dnl Checks for BPF.
tme_raw_type=
AC_CHECK_HEADER(net/bpf.h, [tme_raw_type=bpf])
AC_MSG_CHECKING([for raw Ethernet access method])
if test x$tme_raw_type = x; then
  AC_MSG_ERROR([can't find any raw Ethernet access method])
fi
AC_MSG_RESULT($tme_raw_type)

dnl Configures for the system(s) to emulate.
systems=all
if echo " ${systems} " | grep ' all ' > /dev/null 2>&1; then
  systems="sun2"
fi
TME_MACHINE_SUBDIRS=
TME_IC_SUBDIRS=
TME_ICS=
TME_BUS_SUBDIRS=
for system in $systems; do

    # dispatch on the machine type to get more machines, ICs, and
    # buses to compile:
    case ${system} in

    # the sun2:
    sun2)
	machines="sun sun2"
	ics="m68k am9513 mm58167 z8530"
	buses="multibus"
	;;

    *)
	AC_MSG_ERROR([don't know how to emulate ${machine}])
	;;
    esac

    # add in the new machines, ICs, and buses to compile:
    for machine in $machines; do
	if echo " ${TME_MACHINE_SUBDIRS} " | grep " ${machine} " > /dev/null 2>&1; then :; else
	    TME_MACHINE_SUBDIRS="${TME_MACHINE_SUBDIRS} ${machine}"
	fi
    done
    for ic in $ics; do
	if test -d ic/$ic; then
	    if echo " ${TME_IC_SUBDIRS} " | grep " ${ic} " > /dev/null 2>&1; then :; else
		TME_IC_SUBDIRS="${TME_IC_SUBDIRS} ${ic}"
	    fi
	else
	    ic="tme_ic_${ic}.la"
	    if echo " ${TME_ICS} " | grep " ${ic} " > /dev/null 2>&1; then :; else
		TME_ICS="${TME_ICS} ${ic}"
	    fi
	fi
    done
    for bus in $buses; do
	if echo " ${TME_BUS_SUBDIRS} " | grep " ${bus} " > /dev/null 2>&1; then :; else
	    TME_BUS_SUBDIRS="${TME_BUS_SUBDIRS} ${bus}"
	fi
    done
done
TME_HOSTS="posix bsd"
AC_SUBST(TME_MACHINE_SUBDIRS)
AC_SUBST(TME_IC_SUBDIRS)
AC_SUBST(TME_ICS)
AC_SUBST(TME_BUS_SUBDIRS)
AC_SUBST(TME_HOSTS)

dnl Configure for libtool.
AC_LIBLTDL_INSTALLABLE
AC_SUBST(INCLTDL)
AC_SUBST(LIBLTDL)
AC_LIBTOOL_DLOPEN
AM_PROG_LIBTOOL
AC_SUBST(LTLIBOBJS)
AC_CONFIG_SUBDIRS(libltdl)
TME_PREOPEN=
if test $enable_shared = no; then
  TME_PREOPEN='`sort -u $(top_builddir)/tme-preopen.txt`'
fi
AC_SUBST(TME_PREOPEN)

dnl Configure debugging and/or warnings.
AC_ARG_ENABLE(debug,
[  --disable-debug         don't compile debuggable libraries and programs (default=do)],
[ ], [enable_debug=yes])
if test "x$enable_debug" = "xyes"; then
  CFLAGS="${CFLAGS-} -g -O0"
  CXXFLAGS="${CXXFLAGS-} -g3 -O0"
fi
AC_ARG_ENABLE(warnings,
[  --disable-warnings      don't compile with warnings turned on (default=do)],
[ ], [enable_warnings=yes])
if test "x$enable_warnings" = "xyes" -a "x$GCC" = "xyes"; then
  CFLAGS="${CFLAGS-} -Wall -Werror"
  CXXFLAGS="${CXXFLAGS-} -W"
fi

dnl Generate tmeconfig.h.  The whether-to-generate logic is cribbed 
dnl from glib-1.2.1's configure.in.
dnl See that file for an explanation.
AC_OUTPUT_COMMANDS([
          
## Generate our configure-time sources in two 
## cases:
## 1. `config.status' is run either explicitly, or via configure.
##     Esp. not when it is run in `Makefile' to generate makefiles and
##     config.h
## 2. CONFIG_OTHER is set explicitly
##
## Case 1 is difficult.  We know that `automake' sets one of
## CONFIG_FILES or CONFIG_HEADERS to empty.  This heuristic works
## only when AM_CONFIG_HEADER is set, however.

case "x$CONFIG_OTHER" in
*tmeconfig.h) gen_tmeconfig_h=yes
;;
esac
if test -n "${CONFIG_FILES}" && test -n "${CONFIG_HEADERS}"; then
  # Both CONFIG_FILES and CONFIG_HEADERS are non-empty ==> Case 1
  if test "x${CONFIG_OTHER}" = x; then
    gen_tmeconfig_h=yes
  fi
fi

## If we're generating tmeconfig.h:
if test "x$gen_tmeconfig_h" = xyes; then
  outfile=tmeconfig.h
  echo "$as_me: creating $outfile"
  cat <<TMEEOF > ${outfile}-tmp
/* tmeconfig.h
 * 
 * This is an automatically generated file - please modify 'configure.in'.
 */

#ifndef _TMECONFIG_H
#define _TMECONFIG_H

TMEEOF
  (for word in HAVE_ TIME_ PROTO_ ALIGNOF_ WORDS_BIGENDIAN; do \
    grep $word config.h | sed 's/#define[ 	]\{1,\}/&_TME_/' ; \
  done) | sort | uniq >> ${outfile}-tmp
  echo '#define TME_RELEASE_MAJOR (' \
    `echo $VERSION | sed -e 's,^\([0-9][0-9]*\)\.[0-9][0-9]*$,\1,'` ')' >> ${outfile}-tmp
  echo '#define TME_RELEASE_MINOR (' \
    `echo $VERSION | sed -e 's,^[0-9][0-9]*\.\([0-9][0-9]*\)$,\1,'` ')' >> ${outfile}-tmp
  echo '#define TME_BUILD_TARGET "'$target'"' >> ${outfile}-tmp
  if test $ac_cv_sizeof_int = 4; then
    echo 'typedef signed int tme_int32_t;' >> ${outfile}-tmp
    echo 'typedef unsigned int tme_uint32_t;' >> ${outfile}-tmp
  elif test $ac_cv_sizeof_long = 4; then
    echo 'typedef signed long tme_int32_t;' >> ${outfile}-tmp
    echo 'typedef unsigned long tme_uint32_t;' >> ${outfile}-tmp
  fi
  if test $ac_cv_sizeof_int = 2; then
    echo 'typedef signed int tme_int16_t;' >> ${outfile}-tmp
    echo 'typedef unsigned int tme_uint16_t;' >> ${outfile}-tmp
  elif test $ac_cv_sizeof_short = 2; then
    echo 'typedef signed short tme_int16_t;' >> ${outfile}-tmp
    echo 'typedef unsigned short tme_uint16_t;' >> ${outfile}-tmp
  fi
  echo 'typedef signed char tme_int8_t;' >> ${outfile}-tmp
  echo 'typedef unsigned char tme_uint8_t;' >> ${outfile}-tmp
  cat <<TMEEOF >> ${outfile}-tmp

/* features that aren't currently controlled by a configure option: */
#define TME_THREADS_SJLJ

#endif /* !_TMECONFIG_H */
TMEEOF
  if cmp -s ${outfile}-tmp $outfile; then
    echo "$as_me: $outfile is unchanged"
    rm -f ${outfile}-tmp
  else
    mv ${outfile}-tmp ${outfile}
  fi
fi
], [
PACKAGE="$PACKAGE"
VERSION="$VERSION"
target="$target"
ac_cv_sizeof_int="$ac_cv_sizeof_int"
ac_cv_sizeof_long="$ac_cv_sizeof_long"
ac_cv_sizeof_short="$ac_cv_sizeof_short"
])

dnl Writes files.
AC_OUTPUT(Makefile
	  tme/Makefile
	  tme/ic/Makefile
	  tme/machine/Makefile
	  tme/generic/Makefile
	  libtme/Makefile
	  ic/Makefile
	  ic/m68k/Makefile
	  machine/Makefile
	  machine/sun/Makefile
	  machine/sun2/Makefile
          host/Makefile
          host/posix/Makefile
          host/bsd/Makefile
          bus/Makefile
          bus/multibus/Makefile
          generic/Makefile
	  tmesh/Makefile)

